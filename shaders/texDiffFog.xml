<?xml version="1.0" encoding="UTF-8"?>

<SHADER name="texDiffFog">
	<VERTEX>
	// vertex shader
	// ------------------------------------------------------
	// texture diffuse shader per fragment
	// ------------------------------------------------------
	// lights nb: 1
	// textures nb: 1
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec2 aVertexTexCoord;
		attribute vec3 aVertexNormal;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output
    	varying vec2 vTextureCoord;
		varying vec4 vPosition;
		varying vec3 vNormal;

    void main(void) {
		// vPosition
		vPosition =  uMVMatrix * vec4(aVertexPosition, 1.0);

    	// vNormal
		vNormal = uNMatrix * aVertexNormal;

        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
		// vTextureCoord
        vTextureCoord = aVertexTexCoord;
    }
	</VERTEX>
	<FRAGMENT>
	#ifdef GL_ES
	precision highp float;
	#endif
	// fragment shader
	// ------------------------------------------------------
	// texture diffuse shader per fragment
	// ------------------------------------------------------
	// lights nb: 1
	// textures nb: 1
	// ------------------------------------------------------

	// local constant
		// Kd: diffuse coeff
		float Kd = 0.7 ;
		vec3 FogColor = vec3(0.5, 0.5, 0.5);
		float Ks = 0.5;
		float sh = 8.0 ;
		vec3 specColor = vec3(0.7,0.7,0.5);

	// inputs
    	varying vec2 vTextureCoord;
		varying vec4 vPosition;
		varying vec3 vNormal;

    // Uniform
    	uniform sampler2D uSampler0;

		// lights
		// ambient
		uniform vec3 uAmbientColor;

		//point light ie omni dir:  location, color
		uniform vec3 uPointLightPosition0;
		uniform vec3 uPointLightColor0;


    void main(void) {
    	// light direction
		vec3 lightDirection = normalize(uPointLightPosition0 - vPosition.xyz);
		// eye direction
		vec3 eyeDirection = normalize(-vPosition.xyz);
		// normalize normal
		vec3 normal = normalize(vNormal);
		//  diffuse term
		float diffuse = max(dot(normal, lightDirection), 0.0);
		// spec
		vec3 reflectDirection = normalize(reflect(-lightDirection,normal));
		float specular = pow(max(dot(reflectDirection,eyeDirection),0.0),sh);
		// texture color
		vec4 texColor  = texture2D(uSampler0, vec2(vTextureCoord.s, vTextureCoord.t));
		// light color
		vec3 lightColor =
			Kd*uAmbientColor*texColor.rgb +
			Kd*diffuse*uPointLightColor0 *texColor.rgb +
			Ks*specular*uPointLightColor0*specColor;

		// FOG
		float rangeDist = length(vPosition);
		
		// Exponential Fog
			//float fogValue = 1.0 / exp( (rangeDist * 0.10) * (rangeDist * 0.10) );
			//fogValue = clamp(fogValue, 0.0, 1.0);
			//vec3 color = mix(FogColor, lightColor, fogValue);
		
		// Fog with scattering and height based extinction
			float be = (25.0 - vPosition.y) * 0.01;
			float bi = (25.0 - vPosition.y) * 0.005;
			float ext = exp(-rangeDist * be);
			float sca = exp(-rangeDist * bi);
			vec3 color = lightColor * ext + FogColor * (1.0 - sca);

        gl_FragColor = vec4(color, texColor.a);
    }
	</FRAGMENT>
</SHADER>
